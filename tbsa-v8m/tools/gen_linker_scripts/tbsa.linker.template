/** @file
 * Copyright (c) 2018-2019, Arm Limited or its affiliates. All rights reserved.
 * SPDX-License-Identifier : Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
**/
ENTRY(tbsa_entry)

MEMORY
{
    TBSA_TEXT     (RX)  : ORIGIN = TBSA_TEXT_START, LENGTH = TBSA_TEXT_LENGTH
    TBSA_DATA     (RW)  : ORIGIN = TBSA_DATA_START, LENGTH = TBSA_DATA_LENGTH
    TBSA_NS_DATA  (R)   : ORIGIN = TBSA_NS_DATA_START, LENGTH = TBSA_NS_DATA_LENGTH
    TBSA_NS_TEXT  (RX)  : ORIGIN = TBSA_NS_TEXT_START, LENGTH = TBSA_NS_TEXT_LENGTH
    TBSA_NSC_TEXT (RX)  : ORIGIN = TBSA_NSC_TEXT_START, LENGTH = TBSA_NSC_TEXT_LENGTH
}

SECTIONS
{
    .text :
    {
        __tbsa_text_start__ = .;
        *(.text)
        *(.text*)
        *(.rodata)
        *(.rodata*)

        KEEP(*(.init))
        KEEP(*(.fini))
        __tbsa_text_end__ = .;
    } > TBSA_TEXT

    __tbsa_data_src_start__ = .;

    .data : AT (__tbsa_data_src_start__)
    {
        . = ALIGN(4);
        __tbsa_data_start__ = .;
        *(.data)
        *(.data*)
        . = ALIGN(4);
        __tbsa_data_end__ = .;
    } > TBSA_DATA

    .bss :
    {
        . = ALIGN(4);
        __tbsa_bss_start__ = .;
        *(.bss)
        *(.bss.*)
        *(COMMON)
        . = ALIGN(4);
        __tbsa_bss_end__ = .;
    } > TBSA_DATA

    end = .;

    .tbsa_ns_data :
    {
        . = ALIGN(4);
        __tbsa_ns_data_start__ = .;
        KEEP(*(.tbsa_ns_data))
        . = ALIGN(4);
    } > TBSA_NS_DATA

    .ns_target_database :
    {
        . = ALIGN(4);
        KEEP(*(.ns_target_database))
        . = ALIGN(4);
        __tbsa_ns_data_end__ = .;
    } > TBSA_NS_DATA

    .tbsa_ns_text :
    {
        __tbsa_ns_text_start__ = .;
        *(.text)
        *(.text*)
        *(.rodata)
        *(.rodata*)
        KEEP(*(.tbsa_ns_text))
        __tbsa_ns_text_end__ = .;
    } > TBSA_NS_TEXT

    .tbsa_nsc_entry_points :
    {
        . = ALIGN(32);
        __tbsa_nsc_entry_points_start__ = .;
        KEEP(*(.tbsa_nsc_entry_points))
        . = ALIGN(32);
        __tbsa_nsc_entry_points_end__ = .;
    } > TBSA_NSC_TEXT

    __TBSA_S_StackTop      = ORIGIN(TBSA_DATA) + LENGTH(TBSA_DATA);
    __TBSA_NS_StackTop     = ORIGIN(TBSA_NS_DATA) + LENGTH(TBSA_NS_DATA);
    __TBSA_S_StackLimit    = ORIGIN(TBSA_DATA) + LENGTH(TBSA_DATA) - 0x400;
    __TBSA_NS_StackLimit   = ORIGIN(TBSA_NS_DATA) + LENGTH(TBSA_NS_DATA) - 0x400;

    __tbsa_test_s_start__  = TBSA_TEST_S_START;
    __tbsa_test_s_end__    = TBSA_TEST_S_END;
    __tbsa_test_ns_start__ = TBSA_TEST_NS_START;
    __tbsa_test_ns_end__   = TBSA_TEST_NS_END;

}
